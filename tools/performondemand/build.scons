#!/usr/bin/python2.4
#
# Copyright 2009-2010 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ========================================================================


Import('env')


local_env = env.Clone()
local_env.Append(
    CPPDEFINES = [
        'UNICODE',
        '_UNICODE'
        ],
    CPPPATH = [
        '$OBJ_ROOT',
        ],
    LIBS = [
        ('atls.lib', 'atlsd.lib')[local_env.Bit('debug')],
        ('libcmt.lib', 'libcmtd.lib')[local_env.Bit('debug')],
        ('libcpmt.lib', 'libcpmtd.lib')[local_env.Bit('debug')],
        'comctl32.lib',
        'crypt32.lib',
        'Iphlpapi.lib',
        'mstask.lib',
        'netapi32.lib',
        'psapi.lib',
        'rasapi32.lib',
        'shlwapi.lib',
        'urlmon.lib',
        'userenv.lib',
        'version.lib',
        'wininet.lib',
        'wtsapi32.lib',

        '$LIB_DIR/common.lib',
        '$LIB_DIR/goopdate_dll.lib',
        '$LIB_DIR/logging.lib',
        '$LIB_DIR/net.lib',
        '$LIB_DIR/statsreport.lib',
        ],
)

local_env.FilterOut(LINKFLAGS = ['/SUBSYSTEM:WINDOWS'])
local_env['LINKFLAGS'] += ['/SUBSYSTEM:CONSOLE']

no_precomp_env = local_env.Clone()
idl_obj = no_precomp_env.ComponentObject(
    source='$OBJ_ROOT/goopdate/google_update_idl_i.c')

target_name = 'performondemand'

inputs = [
    'performondemand.cc',
    idl_obj,
    ]
if env.Bit('use_precompiled_headers'):
  inputs += local_env.EnablePrecompile(target_name)

local_env.ComponentTestProgram(
    prog_name=target_name,
    source=inputs,
    COMPONENT_TEST_RUNNABLE=False
)
