# Copyright 2008-2009 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ========================================================================

Import('env')

local_env = env.Clone()
local_env.FilterOut(CPPDEFINES=['_ATL_DEBUG_INTERFACES'])

bcj2_lib = local_env.ComponentStaticLibrary(
    lib_name='bcj2_lib',
    source=[
        'bcj2_encoder.cc',
        'range_encoder.cc',
    ],
)

bin_env = local_env.Clone()
bin_env.FilterOut(LINKFLAGS=['/SUBSYSTEM:WINDOWS'])
bin_env.Append(
    LINKFLAGS=['/SUBSYSTEM:CONSOLE'],
    LIBS=[
        ('libcmt.lib', 'libcmtd.lib')[local_env.Bit('debug')],
        ('libcpmt.lib', 'libcpmtd.lib')[local_env.Bit('debug')],
        'kernel32.lib',
        '$LIB_DIR/lzma.lib',
    ],
)

bin_env.ComponentTool(
    prog_name='bcj',
    source=[
        'bcj.cc',
    ],
)

bjc2_env = bin_env.Clone()
bjc2_env.Append(
    LIBS=[ bcj2_lib ],
)
bjc2_env.ComponentTool(
    prog_name='bcj2',
    source=[
        'bcj2.cc',
    ],
)

local_env.OmahaUnittest(
    name='bcj2_encoder_unittest',
    source='bcj2_encoder_unittest.cc',
    LIBS=[
        bcj2_lib,
        '$LIB_DIR/common.lib',
        '$LIB_DIR/lzma.lib',
    ],
    all_in_one=False,
    COMPONENT_TEST_SIZE='small',
)
